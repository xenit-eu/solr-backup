plugins {
    id 'eu.xenit.docker' version '5.1.1' apply false
    id 'eu.xenit.docker-compose' version '5.1.1' apply false
    id "be.vbgn.ci-detect" version "0.5.0"    
}

def copyPropertyValueIfExists(sourcePropertyName, targetPropertyName) {
    if (project.hasProperty(sourcePropertyName)) {
        project.ext[targetPropertyName] = project.property(sourcePropertyName)
    }
}

subprojects {
    apply plugin: 'java'

    version = '0.0.3' + getVersionQualifier(ci.branch ?: 'local')
    ext.isReleaseVersion = !(version.endsWith("SNAPSHOT"))

    // It is not possible to set properties with a dot via GitHub Actions env variables, therefore we introduce support
    // for a non-dotted-equivalent
    copyPropertyValueIfExists('alfresco_nexus_username', 'org.alfresco.maven.nexus.username')
    copyPropertyValueIfExists('alfresco_nexus_password', 'org.alfresco.maven.nexus.password')

    repositories {
        mavenCentral()
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/public'
        }
        maven {
            url 'https://artifacts.alfresco.com/nexus/content/groups/private'
            credentials {
                username project.property('org.alfresco.maven.nexus.username')
                password project.property('org.alfresco.maven.nexus.password')
            }
        }
    }

    apply from: "${rootProject.projectDir}/publish.gradle"
}

def String getVersionQualifier(String branch_name) {
    if (project.hasProperty('qualifier')) {
        return '-' + project.getProperty('qualifier')
    } else if (branch_name == 'release') {
        return ''
    } else {
        return '-SNAPSHOT'
    }
}
